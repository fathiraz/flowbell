plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics)
}

android {
    namespace = "com.fathiraz.flowbell"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.fathiraz.flowbell"
        minSdk = 26
        targetSdk = 35
        versionCode = 4
        versionName = "0.1.3"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            // Load from local.properties, gradle.properties, or environment variables
            def localProps = new Properties()
            def localPropsFile = rootProject.file('local.properties')
            if (localPropsFile.exists()) {
                localProps.load(new FileInputStream(localPropsFile))
            }

            storeFile file(localProps.getProperty('KEYSTORE_FILE') ?: project.findProperty('KEYSTORE_FILE') ?: 'release.keystore')
            storePassword localProps.getProperty('KEYSTORE_PASSWORD') ?: project.findProperty('KEYSTORE_PASSWORD') ?: System.getenv('KEYSTORE_PASSWORD')
            keyAlias localProps.getProperty('KEY_ALIAS') ?: project.findProperty('KEY_ALIAS') ?: System.getenv('KEY_ALIAS')
            keyPassword localProps.getProperty('KEY_PASSWORD') ?: project.findProperty('KEY_PASSWORD') ?: System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            signingConfig signingConfigs.release
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = "21"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation(libs.core.ktx)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.activity.compose)
    implementation(platform(libs.compose.bom))
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.graphics)
    implementation(libs.compose.ui.tooling)
    implementation(libs.compose.material3)
    implementation(libs.compose.material.icons.extended)
    implementation(libs.navigation.compose)
    implementation(libs.koin.android)
    implementation(libs.koin.compose)
    implementation(libs.timber)
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    implementation("androidx.security:security-crypto:1.1.0-alpha06")
    implementation(libs.splashscreen.compose)
    implementation(libs.lottie.compose)
    
    // Firebase
    implementation(platform(libs.firebase.bom))
    implementation(libs.firebase.crashlytics)
    implementation(libs.firebase.analytics)
    
    // Room dependencies
    implementation(libs.room.runtime)
    implementation(libs.room.ktx)
    ksp(libs.room.compiler)
    
    // Serialization
    implementation(libs.kotlinx.serialization.json)
    
    // HTTP Client
    implementation(libs.okhttp)
    implementation(libs.okhttp.logging)

    // WorkManager
    implementation(libs.work.runtime.ktx)

    // Reveal onboarding
    implementation(libs.reveal.core)
    
    // TODO: Add multi-module dependencies when modules are created
    // implementation(project(":core:common"))
    // implementation(project(":core:ui"))
    // implementation(project(":core:data"))
    // implementation(project(":core:domain"))
    // Unit testing
    testImplementation(libs.junit)
    testImplementation(libs.mockk)
    testImplementation(libs.coroutines.test)
    testImplementation(libs.turbine)
    testImplementation(libs.koin.test)
    testImplementation(libs.koin.test.junit4)

    // Instrumented testing
    androidTestImplementation(libs.junit)
    androidTestImplementation(libs.mockk.android)
    androidTestImplementation(libs.espresso.core)
    androidTestImplementation(platform(libs.compose.bom))
    androidTestImplementation(libs.compose.ui.test.junit4)
    androidTestImplementation(libs.coroutines.test)
    androidTestImplementation(libs.koin.test)
    androidTestImplementation(libs.koin.test.junit4)

    // Debug tools
    debugImplementation(libs.compose.ui.tooling)
    debugImplementation(libs.compose.ui.test.manifest)

    // Beagle debugging tools (modern Hyperion replacement)
    debugImplementation("io.github.pandulapeter.beagle:ui-drawer:2.9.10")
    debugImplementation("io.github.pandulapeter.beagle:log:2.9.10")
    debugImplementation("io.github.pandulapeter.beagle:log-okhttp:2.9.10")
    releaseImplementation("io.github.pandulapeter.beagle:noop:2.9.10")
    releaseImplementation("io.github.pandulapeter.beagle:log-noop:2.9.10")
    releaseImplementation("io.github.pandulapeter.beagle:log-okhttp-noop:2.9.10")

    // Chucker HTTP inspector
    debugImplementation("com.github.chuckerteam.chucker:library:4.1.0")
    releaseImplementation("com.github.chuckerteam.chucker:library-no-op:4.1.0")
}